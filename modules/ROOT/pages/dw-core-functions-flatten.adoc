= flatten
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[flatten1]]
== flatten&#40;Array<Array<T&#62; &#124; Q&#62;&#41;: Array<T &#124; Q&#62;

ネストされた配列 (`[ [1], [2] ]` など) を単純な配列 (`[1, 2]` など) に変換します。


=== パラメータ

[%header, cols="1,3"]
|===
| Name (名前)   | 説明
| `items` | サポートされる任意の型で構成された配列の入力配列。
|===

=== 例

次の例では、配列の配列をフラット化します。

==== 取得元

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
flatten([ [0.0, 0], [1,1], [2,3], [5,8] ])
----

==== 出力

[source,JSON,linenums]
----
[ 0.0, 0 1, 1, 2, 3, 5, 8 ]
----

==== 取得元

次の例では、オブジェクトのネストされた配列から単純な配列を返します。`flatten(myData.user.myarray)` を使用してオブジェクトの配列を選択した後で結果をフラット化するのではなく、`myData.user.myarray` を使用して `myarray` 内のオブジェクトの配列を選択した場合、出力はオブジェクトのネストされた配列になります。

[source,DataWeave, linenums]
----
%dw 2.0
var myData =
{ user : [
   {
     group : "dev",
     myarray : [
       { name : "Shoki", id : 5678 },
       { name : "Mariano", id : 9123 }
     ]
   }
 ]
}
output application/json
---
flatten(myData.user.myarray)
----

==== 出力

[source,JSON,linenums]
----
[
  {
    "name": "Shoki",
    "id": 5678
  },
  {
    "name": "Mariano",
    "id": 9123
  }
]
----

[[flatten2]]
== flatten&#40;Null&#41;: Null

*flatten* を null 値に使用できるようにするヘルパー関数。
